( This file is part of muforth: https://muforth.nimblemachines.com/

  Copyright 2002-2019 David Frech. (Read the LICENSE for details.)

z" THIS CODE IS OBSOLETE! Do not use it!
Use lib/teleprompter2.mu4 instead."  abort

( This is version 1 of the teleprompter code. It was obsolete the moment it
  was written. Use the code in lib/teleprompter2.mu4 instead.)

( This is code to make it easier to paste into the command line, one by one
  and in sequence, previously-written chunks of text. It can be especially
  useful for making screencasts, so that the viewer isn't stuck watching the
  author painfully type and correct.

  The teleprompter interacts with the command line history in the following
  way. Each time ^N is pressed, the next teleprompter line is pasted into the
  command line buffer, but is not executed. It's exactly like what happens
  using the up and down arrow keys to explore the command-line history.

  If a pasted teleprompter command line looks like code, pressing return
  will execute it and enter it into the history buffer. The user has complete
  control. This is an advantage for screencasting use, but a minor
  disadvantage for tutorial use, since the novice user can't tell what is
  explanatory text and what is code to be executed.

  Since command lines can only be 255 characters long it makes sense to use
  a byte-sized - rather than cell-sized - count, just as with command-line
  history.)

: ~  ( compile a new text line)
   #LF parse  ( a u)  255 min
   dup here c!+ ( copy and skip byte count)
   over 1+ allot  ( allot space for count+string; allot pads to cell boundary)
   swap cmove  ( copy into dict) ;

variable tp  ( teleprompter pointer)

: tp-show-next  ( len idx - len' idx')
   2drop  tp @  c@+ swap  ( a len)
   =if  2dup + aligned  tp !  then  ( skip to next prompt, if count != 0)
   tuck  cl-show  dup ;

( Hook into editline code. When we see a ^N, paste in the next prompt.)
-: ( len idx key - len' idx' exit?)
   ctrl N = if  tp-show-next  then  0 ;  is cl-key-unmatched

( Here is an example teleprompter sequence.)

here  tp !
~ In the beginning was the Forth
~ And the Forth was with Chuck
~ And the Forth *was* Chuck
~ (the very short Gospel of Forth)
~ It's possible to execute code as well as show text.
~ Just press return when the command line contains code.
~ ld lib/rational-approx.mu4  phi
~ That last bit of code just calculated two integer factors
~ that when divided yield phi (the golden ratio) to
~ 9 decimal digits of precision!
0 ,

