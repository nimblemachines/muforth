( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

( Handling 16-bit and 32-bit little- and big-endian values.)


( Little endian.)

( Split and join bytes into 16-bit halfwords.)
: >hilo  ( h - hi lo)  dup 8 u>>  swap [ 1 8 << 1- #] and ;
: lohi>  ( lo hi - h)  8 << or ;

( Split and join bytes into 32-bit words.)
: >3210  ( w - b3 b2 b1 b0)  >hilo push  >hilo push  >hilo  pop pop ;
: 0123>  ( b0 b1 b2 b3 - w)   lohi>       lohi>       lohi> ;

( 16-bit halfword fetch and store.)
: leh@+  ( a - h a+2)                    c@+ c@+ push  lohi>  pop ;
: leh!+  ( h a - a+2)  push  >hilo  pop  c!+ c!+ ;

: leh@   ( a - h)  leh@+ drop ;
: leh!   ( h a)    leh!+ drop ;

( 32-bit fetch and store.)
: lew@+  ( a - w a+4)                    c@+ c@+ c@+ c@+ push  0123>  pop ;
: lew!+  ( w a - a+4)  push  >3210  pop  c!+ c!+ c!+ c!+ ;

: lew@   ( a - w)  lew@+ drop ;
: lew!   ( w a)    lew!+ drop ;


( Big endian.)

( Split and join bytes into 16-bit halfwords.)
: >lohi  ( h - lo hi)  >hilo  swap ;
: hilo>  ( hi lo - h)         swap  lohi> ;

( Split and join bytes into 32-bit words.)
: >0123  ( w - b0 b1 b2 b3)            >lohi      >lohi      >lohi ;
: 3210>  ( b3 b2 b1 b0 - w)  push push  hilo>  pop hilo>  pop hilo> ;

( 16-bit halfword fetch and store.)
: beh@+  ( a - h a+2)                    c@+ c@+ push  hilo>  pop ;
: beh!+  ( h a - a+2)  push  >lohi  pop  c!+ c!+ ;

: beh@   ( a - h)  beh@+ drop ;
: beh!   ( h a)    beh!+ drop ;

( 32-bit fetch and store.)
: bew@+  ( a - w a+4)                    c@+ c@+ c@+ c@+ push  3210>  pop ;
: bew!+  ( w a - a+4)  push  >0123  pop  c!+ c!+ c!+ c!+ ;

: bew@   ( a - w)  bew@+ drop ;
: bew!   ( w a)    bew!+ drop ;
