| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

| MIDI notes group into octaves, each starting with C.
|
| Range of the piano (88 keys) is A-1 to C7, inclusive.
|
| C-2  C-1  A-1   C0   C1   C2   C3   C4   C5   C6   C7
|   0   12   21   24   36   48   60   72   84   96  108

| Redefine cr to include a comment prefix (useful, in this case, for
| including in muforth code).

: cr  cr  ." | " ;

| Ten octaves of 12 semitones.
: table-ten-octaves
   cr  ." Octave    C   C#    D   D#    E    F   F#    G   G#    A   A#    B"
   cr  ." ~~~~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~"
   | cr  ." ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
   10 0 do  ( ten octaves)
      cr  i 2 - 4 .r 2 spaces  i 12 *
      12 0 do  ( twelve semitones in each)  i over +  5 .r  loop  drop
   loop ;

| All of the MIDI notes: 0 to 127.
: table-all-midi
   cr  ." Octave    C   C#    D   D#    E    F   F#    G   G#    A   A#    B"
   cr  ." ~~~~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~  ~~~"
   | cr  ." ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
   128 0 do  ( all possible MIDI notes)
      i 12 /mod ( r q)  swap 0= if  cr  dup 2 -  4 .r  2 spaces  then  drop
      i 5 .r
   loop ;

: on-piano?  ( note# - f)  21 109 within ;

| Assuming each chip can handle 6 notes - eg, each chip has 6 hardware PWM
| outputs - apportion notes to chips. Also assuming that each board handles
| an octave - 12 semitones - print each board index as well.

: chips  ( board-origin)  | this is 12 (C-1) or 21 (A-1)
   109 over do  ( board origin to end of piano range)
      i over -  12 /mod ( r q)  over 0= if  cr cr  ." Board "  dup 1 .r  then  drop
                                    6 = if     cr  ."        "           then
      i over -   6 /mod ( r q)  swap 0= if  ."  | Chip "  dup 2 .r  ." :"  then  drop
      i dup on-piano? if  5 .r  else  drop 5 spaces  then
   loop  drop ;
