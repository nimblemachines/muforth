( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

( Sieve of Eratosthenes)

( line by line printing)
variable .width.  80 .width. !
variable .col.  .width. @ .col. !

: type_
   dup 1+ dup .col. @ +  .width. @ u< not if  cr .col. off then
   .col. +!  type space ;


1,000,000 constant nodds  ( number of odd numbers, starting with 3, to check)
nodds 7 + 3 >>  buffer seen-bits

: i>bit  ( i - byte bit)  dup 3 >> swap 7 and ;
: 'seen  ( i)  i>bit  1 swap << ( mask)  swap  seen-bits +  ( mask a) ;
: seen   'seen  dup c@  rot or  swap c! ;
: seen?  'seen  c@  and ;
: 1mark  ( prime i - prime i')  dup seen  over + ;
: sieve  ( i prime)  swap  begin  dup nodds <  while  1mark  repeat  2drop ;
: i>odd  ( map index to odd #)  2* 3 + ;
: print   (.) type_ ;
: prime?   ( found i - found' i+1)
   dup seen? not if  dup  dup i>odd  dup print  sieve  1 u+  then  1+ ;
: erat   2 print  ( this one is special)
   0  0  nodds for  prime?  next drop  cr ." (" . ." primes)" ;

>stdout  ( so we can redirect to a file)
erat
>stderr bye
