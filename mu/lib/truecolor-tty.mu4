( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

ld lib/solarized.mu4

loading Truecolor (24-bit color) support for terminals

( Many terminal emulator programs now support setting both the foreground
| and background text color using 24-bit color *literals*, rather than
| specifying an *index* into a table of 8, 16, or 256 colors. This is a
| *huge* improvement. The biggest problem with the lookup tables is that
| there generally is no way to *set* the color values from within a
| program - a text editor, say. If you wanted to explore using a particular
| color scheme - Solarized, for example - it meant coordinating between the
| editor and the terminal emulater to get the color table set up properly.
| It doesn't sound that hard, but it still caused endless headaches.
|
| As a personal example, I only got Solarized working with Vim once I
| realized that I could add truecolor support to Vim instead of relying on
| the "underlying" color table administered by the terminal emulator.
|
| And, amazingly, this works on "hterm" - the terminal program for
| Chromebooks.
|
| This is yet another example of legacy crap from the 1980s making it hard
| to do something that should be trivially simple: display text in a
| particular color.
|
| Unfortunately, even at this late date - it is 2018 as I write this -
| there is no standard way to set these 24-bit colors, or to query if a
| particular terminal or emulator can handle them.
|
| There is a lot more information about this here:
|
|    https://gist.github.com/XVilka/8346728
|
| Assuming your terminal [emulator] can handle it, let's try it out!
|
| The foreground escape sequence is ESC[38;2;<r>;<g>;<b>m
| The background escape sequence is ESC[48;2;<r>;<g>;<b>m
| <r> <g> <b> range from 0 to 255 inclusive.
| The escape sequence ESC[0m returns output to default.
)

( Use "pictured numerics" to create the string at pad.)

: "m   char m hold ;
: ";   char ; hold ;
: ;#   ( component)  #s drop  "; ;
: (rgb)  ( rgb which)  radix preserve decimal  push  >rgb
   <#  "m  ;# ;# ;#  2 ;#  pop #s  " [" "hold  #> ;

: (fg)  ( rgb - a u)  38 (rgb) ;
: (bg)  ( rgb - a u)  48 (rgb) ;

: fg  (fg) type ;
: bg  (bg) type ;

: fg0  ( reset to normal)  ." [0m" ;

( 255,100,0 => hex ff6400)
: test  "ff6400 fg  ." TRUECOLOR"  fg0 ;

( For fun, let's "prettyprint" - with color - a C routine from muforth's
| implementation. This is from muforth/src/interpret.c.)

: hi  ( hilight)  ( color)  constant  does>  ( a u)  @ fg type ;
base0  hi xnorm  ( normal text)
yellow hi xtype
base01 hi xcomment
green  hi xres   ( reserved word)
red    hi xspec  ( special)
blue   hi xnum

: solar  cr
" /*
 * Scan for trailing delimiter and consume it, unless we run out of
 * input text first.
 */
" xcomment
" static void " xtype  " scan("  xnorm  " int "  xtype  " delim)
{
" xnorm
"     char " xtype  " *last;
" xnorm
"     char " xtype  " c;

" xnorm
"     /* capture lineno that token begins on */
" xcomment
"     parsed_lineno = lineno;

" xnorm
"     for " xres  " (last = _(first); last < _(end); last++)
    {
        c = *last;
" xnorm
"         if " xres  " (c == " xnorm  " '\n'" xspec  " ) lineno++;
" xnorm
"         if " xres  " (delim == c
            || (delim == ' ' && isspace(c)))
        {
" xnorm
"             /* found trailing delimiter; consume it */
" xcomment
"
            capture_token(last, " xnorm  " 1" xnum  " );
" xnorm
"             return" xres  " ;
        }
    }

" xnorm
"     /* ran out of text; don't consume trailing */
" xcomment
"     capture_token(last, " xnorm  " 0" xnum  " );
}
" xnorm ;
