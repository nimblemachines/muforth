( This file is part of muforth: http://muforth.nimblemachines.com/

  Copyright 2002-2017 David Frech. (Read the LICENSE for details.)

( We need these words to make dumping memory, decompiling code, and
  disassembling more beautiful. Eventually all console i/o should be
  in raw mode...)

z" /dev/tty" open-file-rw  constant tty

create cooked-termios  tty here get-termios aligned allot

: user-raw  ( fd)  ( This is tailored for human interaction)
    dup  here get-termios drop   here set-termios-user-raw
  ( fd)  here set-termios  ( set to raw!) ;

: cooked  ( fd)   cooked-termios  set-termios ;

( common key names)
ctrl H  constant #BS   --   8
ctrl [  constant #ESC  --  27
ctrl ?  constant #DEL  -- 127

defer key-timeout
-:  error" <key timed out" ;  is key-timeout

: <key   ( fd - ch)
   charbuf 1 read
   1 = if  charbuf c@ ^  then  ( read a character successfully)
   key-timeout ;

: key    ( - ch)   in-channel @ @ ( fd)  <key ;
