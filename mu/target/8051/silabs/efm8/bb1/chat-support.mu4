( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

( Chat support for Silabs EFM8BB1.)

__meta
hex

label device-init
   0 # CLKSEL mov             -- At reset, running at HFOSC/8. Let's set it to /1.
   80 ( VDMEN) # VDM0CN orl   -- Enable power supply monitor
   2 ( PORSF) # RSTSRC mov    -- Allow power supply monitor to cause resets

   -- Init UART.
   -- Baud rate from Timer1, 8-bit mode, auto-reload (mode 2)
   8 ( T1M) # CKCON0 orl      -- timer 1 clocked from sysclk/1
   TCON .6 setb               -- turn on timer 1
   20 # TMOD orl              -- timer1 mode 2

   -- set timer to overflow at twice desired baud rate
   #24,500,000 #115,200 \f / 2/ negate # TH1 mov
   12 ( REN+TI) # SCON0 orl   -- enable recv, set TI

   -- Connect UART to port pins 0.4 and 0.5.
   01 # XBR0 mov              -- connect UART
   40 # XBR2 mov              -- enable crossbar
   ( fall thru)  ;c
