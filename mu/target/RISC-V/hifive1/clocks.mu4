( This file is part of muforth: https://muforth.nimblemachines.com/

  Copyright 2002-2023 David Frech. (Read the LICENSE for details.)

loading HiFive1 clock generation experiments

__meta

( mtime is a 64-bit counter, clocked by the LFCLK - which, in the case of
  the HiFive1, is driven by a 32768 Hz external oscillator.

  mtime @  gives us the low 32 bits, which is all we need.)

: ticks  ( n)  mtime @  + begin  mtime @  over - 0< 0= until  drop ;

( If we run the chip for 32768 lfclk cycles - exactly one second - we can
  measure the speed by reading the mcycle counter. It will tell us how many
  cycles we executed in a second.)

: speed
   mcycle32@  32768 ticks  mcycle32@  swap - ;

: modify-trim  ( trim cfg - cfg')
   "001f_0000 bic        ( clear trim bits)
   swap 31 and 16 << or  ( or in new trim bits) ;

( Let's trim the HFROSC, fire it up, and then switch to it.)
: >hfrosc
   hfrosc-trim @  "4000_0000 ( enable, /1)  modify-trim  hfrosccfg !
   begin  hfrosccfg @  0< until  ( until ready bit)
   "1_0000 ( pllsel) pllcfg -bits  ( hfrosc drives hfclk) ;

( To test out trims other than the one stored in OTP.)
: retrim-hfrosc  ( trim)
   hfrosccfg @  modify-trim  hfrosccfg !  ( write new trim) ;

( Now let's fire up the PLL, and switch to it. Let's go for 320 MHz!)
: >pll
   >hfrosc  ( switch to internal osc while fiddling with PLL)
   "100 plloutdiv !  ( /1)
   (         Q/2  F/80    R/2 )
   %0010_0000_01_100111_0_001 pllcfg !  33 ticks ( about 1ms)
   begin  pllcfg @  0< until  ( wait until PLL lock)
   "1_0000 ( pllsel) pllcfg +bits  ( PLL drives hfclk) ;

( In case we want to return to a more "modest" state, we can go back to
  using the 16 MHz HFXOSC output as the HFCLK.)
: >hfxosc
   >hfrosc  ( switch to internal osc while fiddling with PLL and XOSC)
   "4000_0000 ( enable)  hfxosccfg !  ( turn HFXOSC on)
   begin  hfxosccfg @  0< until  ( until ready bit)
   "6_0000 ( pllbypass+pllrefsel) pllcfg +bits  ( bypass PLL, use HFXOSC as pllref)
   1 ticks  ( wait a bit, just to be sure)
   "1_0000 ( pllsel) pllcfg +bits  ( HFXOSC drives hfclk - via bypassed PLL) ;
