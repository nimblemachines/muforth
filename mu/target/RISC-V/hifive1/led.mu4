( This file is part of muforth: https://muforth.nimblemachines.com/

  Copyright 2002-2019 David Frech. (Read the LICENSE for details.)

loading HiFive1 LED experiments

__meta

( Let's create these constants on the host:)
1 19 << equ green
1 21 << equ blue
1 22 << equ red
red green      +   equ yellow   ( aka -blue)
red       blue +   equ magenta  ( aka -green)
    green blue +   equ cyan     ( aka -red)
red green blue + + equ white

( XXX Should use AMOxxx instructions here...)
: +bits  ( bits a)   ( turn on bits in a word in memory)
   tuck               @ or  swap ! ;

: -bits  ( bits a)   ( turn off bits in a word in memory)
   swap  invert  over @ and  swap ! ;

: on   ( color)   gpio.port +bits ;
: off  ( color)   gpio.port -bits ;
: set  ( color)   white off  on ;

: led-init
   white gpio.iof_en    -bits  ( make sure GPIO controls the LEDs)
   white gpio.output_en +bits  ( LED pins are outputs)
   white gpio.out_xor   +bits  ( 1 means ON)
   white off ;
