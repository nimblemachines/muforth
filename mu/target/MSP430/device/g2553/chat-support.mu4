| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

loading MSP430 serial chat (G2553 support)

hex
__meta

label get-byte
   begin  01 # IFG2 & bitb  0!= until
   UCA0RXBUF & top movb  ret  ;c

label put-byte
   begin  02 # IFG2 & bitb  0!= until
   top UCA0TXBUF & movb  ret  ;c

.ifndef in-ram

label device-init
   | If there is a 16 MHz calibration value in the info area, use it and set
   | the DCO to generate 16 MHz MCLK and SMCLK.

   "0ffff # CAL_DCO_16MHZ & cmp     | if Info A has been erased, don't use trim
   0= if
      | Set defaults that might work to get chat going...
      %10_00_1111 # BCSCTL1 & movb  | RSEL=15
      a0 # DCOCTL & movb            | not quite the middle?
   else
      | Set clock to factory-calibrated 16 MHz
      | See G2553 datasheet, p15
      CAL_BC1_16MHZ & BCSCTL1 & movb
      CAL_DCO_16MHZ & DCOCTL & movb
   then
   BCSCTL2 & clrb

   | Set up flash clock; needs to be between 257 and 476 kHz (no joke)
   | We're running at 16M, so set it to 16M/40 = 400k.
   FKEY #39 ( divisor-1) +  %0100_0000 ( MCLK)  + # FCTL2 & mov
   ret  ;c

.then  ( not in-ram)

label uart-init
   | Configure UART for 115,200 bps, 8 bits, no parity
   81 # UCA0CTL1 & bisb    | enable SMCLK
                           | hold UART in reset while configuring
   UCA0CTL0 & clrb         | reset to defaults: 8 bits, no parity, 1 stop
   UCA0BR1 & clrb          | set baud rate divisor (high)

   ( Baud rate divisor [low] and modulation bits.)
.ifndef slow-chat
   8 # UCA0BR0 & movb            | 16,000,000/16/115,200
   %1011_0001 # UCA0MCTL & movb  | ie, UCBRF0=11 (decimal), UCBRS0=0, UCOS16=1
.else
   #104 # UCA0BR0 & movb         | 16,000,000/16/9600
   %0011_0001 # UCA0MCTL & movb  | ie, UCBRF0=3, UCBRS0=0, UCOS16=1
.then

   01 # UCA0CTL1 & bicb    | bring UART out of reset

   ( Connect P1.2 to UART TXD, P1.1 to UART RXD. SEL=1 & SEL2=1)
   %0000_0110 # P1SEL & bisb
   %0000_0110 # P1SEL2 & bisb
   ret  ;c
