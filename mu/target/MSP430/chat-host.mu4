| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

| Load host-side support for chat.
|
| We define some common code, load the version-specific code, then end with
| code to connect the chat code with the interact code.

| Common code shared across chat protocols.

: >b   send ;
: b>   recv ;

: >w         >hilo  >b >b ;
: w>   b> b>  lohi> ;

: cx.Idle   0 >b ;
: >cmd   ?spkt  >b ;

( Send two no-ops, let them transmit, _then_ throw away any input bytes.)
: resync  cx.Idle  cx.Idle  drain  flush ;

.ndef chat1-host  .ndef chat2-host  .ndef chat4-host  .ndef chat5-host  .and .and .and .if
   | If no override, set version based on chip.
   .ifdef g2553  -d chat2-host  .else  -d chat4-host  .then
.then

.def chat1-host  .def chat2-host  .or .if
   ld target/MSP430/chat2-host.mu4
.else .ifdef chat5-host
   ld target/MSP430/chat5-host.mu4
.else
   ld target/MSP430/chat4-host.mu4
.then .then

| Connect chat with the interact code.

: c.Hello  ( - chat-running-in-ram? chunk-size)
   #115200 bps  resync
   cr ." Chat firmware version "
   get-version  radix preserve  hex  <# 4# 4# #> type
   get-chat-pc @flash u<  dup if  ."  (running in RAM)"  then
   #256 ;

: chat
   chat-via  c.Hello  c.Status  c.Run  c.ReadChunk  c.WriteChunk
             c.ErasePage  c.FlashChunk ;
