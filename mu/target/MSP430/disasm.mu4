| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

.ifndef prefix-disasm
loading MSP430 disassembler (postfix)
.else
loading MSP430 disassembler (prefix)
.then

| Disassembler for MSP430 CPU instructions. There is _no_ support for CPUX
| instructions! They will disassemble as *unimplemented*.

hex

| Words to compile the instruction table.
| ..  acts like 'then'
| match and exact compile a test and a zbranch, followed by code to print
| the opcode name

: (match)   ( op mask match - op f)  push  over and  pop = ;
: (exact)   ( op      match - op f)        over          = ;

( In the disassembler, use .op to print the opcode name.)
: op,   compile (lit)  .assembler. \chain ;  ( compile op as literal)
: .op   ( cfa)  >name type ;  ( print op name from cfa)

compiler
: ..   compile ^  \ then ;
: multi  ( - src)  compile (match)  \ if                   ;  ( no literal op)
: match  ( - src)  compile (match)  \ if  op,              ;  ( literal op)
: exact  ( - src)  compile (exact)  \ if  op,  compile .op ;  ( literal op, print)
forth

( Sign-extend 16-bit word to host number)
: sext  ( w - n)   dup 8000 and if -1_0000 + then ;

( Standard ways of printing different "types".)
: .udec   radix preserve  decimal  (u.) type ;
: .hex    radix preserve  hex       (.) type ;

defer .regname  ( so others can override with their particular convention)

-: ( reg)
   dup 3 u< if  ( named)
      2*  z" pcspsr"  + 2 type  ^  then
   ." r"  .udec ;  is .regname


| We would like to print "significant" addresses - like equates - in a
| symbolic way, both here in the disassembler and in the decompiler. Let's
| define a few utility words to make that easier.

| XXX is this the right name? It goes from a link to a name of *any* kind
| of word in the dict, not just target words.
|
| .name-from-link ??

: .target  ( 'link)  link>name type ;
: .equate  ( 'link offset)  swap .target  ?if  ."  + " .udec  then ;

| Let's be smart about equates. Most equates are multiples of two. If we
| are using a byte offset, we won't match the equate. We need to match the
| aligned value and then print the difference, if any. But some equates are
| *not* multiples of two; for these we want to look for an exact match.
|
| Let's try to match equates *twice*: the first time we look for an exact
| match; the second time for a two-byte-aligned match, which we will print
| with an offset, if any.

: equate?  ( a - 'link offset -1 | 0)
   ( First, look for an exact match.)
   dup .equates. find-constant if  nip  0 -1  ^  then

   ( If that fails, looked for an aligned match.)
   dup -2 and ( addr aligned-addr)
   .equates. find-constant if  swap  1 and  -1  ^  then
   drop  0 ;

| If address is found in equates, print its *name*; otherwise, just print
| the hex value.

: .equate-or-hex
   dup equate? if  .equate drop ^  then  .hex ;

| Do the same search for labels that we did for equates, but without the
| "fuzzy" matching.

: label?  ( addr - 0 | 'link -1)  .labels. find-constant ;


| Remember, 2op instructions have this form:
| oooo_ssss_DbSS_dddd   o=op, s=srcreg, d=destreg, S=srcmode, D=destmode
|
| We convert to a "canonical" ea with the form:
| 0000_0000_00RR_rrrr   r=reg, R=mode

: src>ea   ( op - ea)
   dup 0f00 and 8 >>  swap  30 and        or ;

: dest>ea  ( op - ea)
   dup   0f and       swap  80 and  3 >>  or ;


.ifndef prefix-disasm  ( default to postfix version!)

: .o  ( off)  .hex ;
: .i  ( imm)  .hex  ." #" ;

: .pcrel      p @  cell* sext  +  .equate-or-hex ;  ( no & suffix)
: .absolute        cell*          .equate-or-hex  ."  &" ;

: .reg       ( ea)            ea>reg  .regname ;
: .indirect  ( ea)            ea>reg  .regname ."  )"  ;
: .indexed   ( ea)  cell* .o  ea>reg  .regname ."  +)" ;
: .postinc   ( ea)  .indirect  ." +" ;

.else ( prefix disassembler)

: .o  ( off)         .hex ;
: .i  ( imm)  ." #"  .hex ;

: .pcrel      p @  cell* sext  +  .equate-or-hex ;  ( no & prefix)
: .absolute        cell*    ." &" .equate-or-hex ;

: .reg       ( ea)  ea>reg        .regname ;
: .indirect  ( ea)  ea>reg  ." (" .regname ." )" ;
: .indexed   ( ea)  cell* .o  .indirect ;
: .postinc   ( ea)  .indirect  ." +" ;

.then


: .eadest  ( ea)
   dup 10 = if  drop  .pcrel     ^  then
   dup 12 = if  drop  .absolute  ^  then
   dup  4 >> jump  .reg  .indexed  .indirect  .postinc  [  show

( XXX should be some kind of table?)
: .easrc   ( ea)
   dup 30 = if  drop  cell* .i  ^  then
   dup 22 = if  drop      4 .i  ^  then
   dup 32 = if  drop      8 .i  ^  then
   dup 03 = if  drop      0 .i  ^  then
   dup 13 = if  drop      1 .i  ^  then
   dup 23 = if  drop      2 .i  ^  then
   dup 33 = if  drop  0ffff .i  ^  then
   .eadest ;

: .byte  ( op - op)  dup 40 and if ." b" then ;

: >jmpdest  ( op - dest)
   dup 1ff and  swap 200 and -  2*  p @  + ;

: .jmpdest  ( addr)
   dup label? if  nip .target ^  then  .hcell ;

( Print jmp opcode.)
: .jop  ( op - op)
   z" jnz jz  jnc jc  jn  jge jl  jmp "  over #10 >>  7 and  2* 2*  +
   3 -trailing  type ;


.ifndef prefix-disasm  ( default to postfix version!)

( 1ops have byte suffix already.)
: .1op   ( op cfa - op)  over ea>rm    .easrc  space  .op ;
: .dest  ( op cfa - op)  over dest>ea  .eadest space  .op ;  ( for synthetic instrs)

( 2ops need to add byte suffix.)
: .2op   ( op cfa - op)
   over src>ea .easrc space  over dest>ea .eadest space  .op .byte ;

: .jmp  ( op - op)  dup >jmpdest .jmpdest space  .jop ;

.else ( prefix disassembler)

( 1ops have byte suffix already.)
: .1op   ( op cfa - op)  .op space  dup ea>rm    .easrc ;
: .dest  ( op cfa - op)  .op space  dup dest>ea  .eadest ;  ( for synthetic instrs)

( 2ops need to add byte suffix.)
: .2op   ( op cfa - op)
   .op .byte space  dup src>ea .easrc space  dup dest>ea .eadest ;

: .jmp  ( op - op)  .jop space  dup >jmpdest .jmpdest ;

.then


: shred  ( op - op)
( All the instructions!)

( Match some "synthetic" instructions before we try the core instructions.)
        4303 exact nop
..      d312 exact setc
..      d322 exact setz
..      d222 exact setn
..      d232 exact eint
..      c312 exact clrc
..      c322 exact clrz
..      c222 exact clrn
..      c232 exact dint

| XXX is it better to disassemble these as pop instructions, or as mov from
| sp, post-incremented?

.. ff70 4130 match pop   .dest
.. ff70 4170 match popb  .dest

( These are the real, core instructions.)

.. ffc0 1000 match ror    .1op
.. ffc0 1040 match rorb   .1op
.. ffc0 1080 match swpb   .1op
      ( 10c0)
.. ffc0 1100 match asr    .1op
.. ffc0 1140 match asrb   .1op
.. ffc0 1180 match sxt    .1op
      ( 11c0)
.. ffc0 1200 match push   .1op
.. ffc0 1240 match pushb  .1op
.. ffc0 1280 match call   .1op
      ( 12c0)
.. ffc0 1300 match reti   .1op
      ( 1340)
      ( 1380)
      ( 13c0)

.. e000 2000 multi       .jmp

.. f000 4000 match mov   .2op
.. f000 5000 match add   .2op
.. f000 6000 match addc  .2op
.. f000 7000 match subc  .2op

.. f000 8000 match sub   .2op
.. f000 9000 match cmp   .2op
.. f000 a000 match dadd  .2op
.. f000 b000 match bit   .2op

.. f000 c000 match bic   .2op
.. f000 d000 match bis   .2op
.. f000 e000 match xor   .2op
.. f000 f000 match and   .2op

.. ." *unimplemented*"

( That's all folks!)
;

( Support for interactive disassembly.)
: dis+  ( a - a' 0)  drop  p @  0 advance  0 ;
: dis-  ( a - a' 0)            -4 advance  0 ;  ( back up a bit)

( The workhorse.)
: 1dis  ( a)
   dup label? if
      over _addr .nesting space  ." label " .target  then
   dup .addr  .nesting space
   p !  cell*  ( op)  dup .hcell_ space
   ."   "  shred  drop ;
