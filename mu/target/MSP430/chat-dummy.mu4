| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

loading MSP430 chat (dummy implementation)

| NOTE: All the following d.Foobar commands are the dummy _implementations_
| of the chat command interface.

variable dummy-pc   @boot dummy-pc !  ( as if running chat in flash)
variable dummy-flash-status

#image buffer dummy-image

: dummy-image+  ( target - host)  wrap  dummy-image + ;
: dummy-image-  ( host - target)        dummy-image -  wrap ;

: d.Hello  ( - chat-running-in-ram? chunk-size)
   cr ." Connecting to dummy chat target"
   dummy-image #image "ff fill  ( erase)
   dummy-flash-status off
   dummy-pc @ @boot u< dup if  ."  (running in RAM)"  then
   #256 ;

: d.ReadChunk    ( buf a u)
   | cr  ." Dummy read chunk "  2 nth u.  over u.  dup u.
   push  dummy-image+  swap  pop cmove ;

: d.WriteChunk   ( buf a u)
   | cr  ." Dummy write chunk "  2 nth u.  over u.  dup u.
   push  dummy-image+  pop cmove ;

: d.ErasePage   (     a   - fail?)
   dummy-image+  /page  "ff fill
   dummy-flash-status @ ;

: d.FlashChunk  ( buf a u - fail?)
   d.WriteChunk   dummy-flash-status @ ;

: d.Status   ( - cp)   "d0d0 ;
: d.Run      ( pc cp)  drop  dummy-pc ! ;

: chat
   chat-via  d.Hello  d.Status  d.Run  d.ReadChunk  d.WriteChunk
             d.ErasePage  d.FlashChunk ;
