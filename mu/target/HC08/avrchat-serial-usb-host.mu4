( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2017 David Frech. (Read the LICENSE for details.)

loading AVR chat over serial over USB (host)

( Host-side code to drive the AVR serial chat code in
  target/HC08/avrchat-serial-usb-core.mu4.)

hex

-- : usb  ( bmRequest bRequest wValue wIndex wLength 'buffer)

( Commands:
0-5f  Bye        - exit chat code and return to chat loop
  60  SetSpeed   - set baud rate divisor, assuming 24M clock
  61  Read       - send cmd, then read data bytes
  62  Write      - send cmd, then write data bytes
)

( "avruu" prefix means AVR uart over USB.)

( Control reads)
: avruu.Read  ( cmd len buf)  2push  push
   0c0  61  pop  0  2pop  usb ;

( Control writes)
: avruu.Speed  ( divisor)  push
    40  60  pop  0   0 0  usb ;

: avruu.Write  ( cmd len buf)  2push  push
    40  62  pop  0  2pop  usb ;

: avruu.Bye
    40  0  0 0  0 0  usb ;

: avruu.Hello  ( start the AVR serial chat firmware on the connected device)
   @avrchat-serial u.SetPC u.Run ;


( The following is code to talk to my simple "debug stub" running on an
  Atmel AVR, and communicating via a serial port running at 125k.)

: uread   ( len cmd)  swap  pad  avruu.Read ;
: uwrite  ( len cmd)  swap  pad  avruu.Write ;

( Because we're using the Z register on the AVR as a memory address
  pointer, let's prefix these words with 'z'. It's easy to type and sounds cool.)

( Here and elsewhere we use * to mean "fetch byte with post-increment",
  cell* to mean "fetch cell with post-increment", and & to be the store
  versions of these)

: zync                     0  00 uwrite ;  ( no-op; flush command)
: z!  ( addr)    zync  zync
                 pad leh!  2  10 uwrite ;
: zp*      ( - b)          1  11 uread  pad c@ ;    ( program memory)
: z*       ( - b)          1  12 uread  pad c@ ;
: z&       ( b)  pad c!    1  13 uwrite ;
: zgo                      0  14 uwrite ;  ( jump to addr in Z)

: zpcell*  ( - w)   zp* zp*  lohi> ;
: zcell*   ( - w)   z*  z*   lohi> ;
: zcell&   ( w)              >hilo  z&  z& ;

( Standard forms for interact - we might also be using the TWI chat.)
: avrchat.Hello     avruu.Hello ;
