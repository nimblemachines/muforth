| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

( Generic von Neumann device with a 32-bit address space.)

( Define the basics of the target's memory.)
4 constant bytes/cell
-d target-comma-aligns  ( assume true for 32-bit targets)

ld target/common/memory.mu4   ( generic memory image support)

( Define a single 32-bit address space.)
32 make-aspace

( Define the flash and ram images, and in each image create a single region.)
( Allocate 2 MiB for flash, and 512 Ki for ram.)

dup   2 Mi make-image  make-region flash
    512 Ki make-image  make-region ram

| We need to be able to map from an address in our aspace to one of the
| regions that it contains.

| within-ram? tests whether a target address is within the bounds of the
| ram and returns a flag.
|
| XXX This currently checks if the address falls within the bounds of the
| current *device's* ram, but wouldn't this work just as well to use the
| size of the ram *image*, as defined above?

: within-ram?  ( target - flag)  @ram [ @ram #ram + #] within ;

( If address within ram region, choose ram; otherwise, flash.)
-: ( target)  ( find a region based on target addr)
   within-ram? if  ram ^  then  flash ;

| Install this chooser into our aspace. We need to have set a region for
| this to work. It doesn't matter which one; they share the aspace.

ram 'choose-image !

( Initialization.)
: fill-image   'image #image rot fill ;  ( fills *current* image)
: wipe
   flash  @flash region!  "ff fill-image
   ram    @ram   region!    0 fill-image ;

wipe  ( leaves us in ram!)
