| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

| loading Serial expect/send

( Simple target serial expect/send code.)

| XXX Should target-tty be called serial-target?

z" serial-target"  r/w open-file?  .if  constant target-tty  .else  drop
z" serial-target2" r/w open-file?  .if  constant target-tty  .else  drop  .then .then

.ifdef target-tty

( Modify termios settings of target-tty device.)
: target-termios   create  ( cfa)  ,  does> @  target-tty  modify-termios ;

( Set up raw mode tailored for target interaction.)
' set-termios-target-raw      target-termios target-raw

' set-termios-speed           target-termios bps  ( speed)
' set-termios-ignore-parity   target-termios ignore-parity
' set-termios-even-parity     target-termios even-parity
' set-termios-odd-parity      target-termios odd-parity

( Recv from, send to target.)
: _send  target-tty  >emit ;
: _recv  target-tty  <key ;

| flush throws away bytes in the input queue; drain waits until all bytes
| in the output queue have been transmitted.

: flush  target-tty  tty-iflush ;
: drain  target-tty  tty-drain ;

| Clear the decks.
target-raw  flush  drain

( Spying on the protocol.)
variable spy  spy off
: send          spy @ if ." >"  dup .h8_ then  _send ;
: recv   _recv  spy @ if ." <"  dup .h8_ then ;

| To make spying on the protocol easier to deal with, break up the printed
| bytes into "packets" in a semantically meaningful way. How about this
| stupid name, which is easy to remember:

: ?spkt  ( spy packet, or "spiked")  spy @ if  cr  then ;

| Wait for target command loop to announce itself, and compare to what we
| expect. This has only ever been used to identify firmware running on a
| serially-connected S08 device.

: await  ( a u)
   m preserve
   swap m !  for  m* recv  xor if  error" Wrong firmware"  then  next ;

.then  ( ifdef target-tty)
