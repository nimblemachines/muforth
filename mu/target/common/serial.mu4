| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

( Simple target serial send/recv code.)

z" serial-target"  r/w open-file?  .if  constant serial-target  .else  drop
z" serial-target2" r/w open-file?  .if  constant serial-target  .else  drop

| Better names and more options?
z" serial-target-2" r/w open-file?  .if  constant serial-target  .else  drop
z" serial-target-3" r/w open-file?  .if  constant serial-target  .else  drop
z" serial-target-4" r/w open-file?  .if  constant serial-target  .else  drop
.then .then .then .then .then

.ifdef serial-target

serial-target constant target-tty   | DEPRECATED; defined for compat reasons

( Modify termios settings of serial-target device.)
: target-termios   create  ( cfa)  ,  does> @  serial-target  modify-termios ;

( Set up raw mode tailored for target interaction.)
' set-termios-target-raw      target-termios target-raw

' set-termios-speed           target-termios bps  ( speed)
' set-termios-ignore-parity   target-termios ignore-parity
' set-termios-even-parity     target-termios even-parity
' set-termios-odd-parity      target-termios odd-parity

( Recv from, send to target.)
: _send  serial-target  >emit ;
: _recv  serial-target  <key ;

| flush throws away bytes in the input queue; drain waits until all bytes
| in the output queue have been transmitted.

: flush  serial-target  tty-iflush ;
: drain  serial-target  tty-drain ;

| Set the serial target device to raw mode and "clear the decks".
target-raw  flush  drain

( Spying on the protocol.)
variable spy  spy off
: send          spy @ if ." >"  dup .h8_ then  _send ;
: recv   _recv  spy @ if ." <"  dup .h8_ then ;

| To make spying on the protocol easier to deal with, break up the printed
| bytes into "packets" in a semantically meaningful way. How about this
| stupid name, which is easy to remember:

: ?spkt  ( spy packet, or "spiked")  spy @ if  cr  then ;

| This is code specific to the S08. Various firmware routines that might be
| programmed into an S08 have a a 5-byte "signature". await can be used to
| match that signature, to make sure the host is communicating with the
| right piece of code.
|
| Wait for target command loop to announce itself, and compare to what we
| expect. This has only ever been used to identify firmware running on a
| serially-connected S08 device.

: await  ( a u)
   m preserve
   swap m !  for  m* recv  xor if  error" Wrong firmware"  then  next ;

.then  ( ifdef serial-target)
