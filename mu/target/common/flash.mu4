| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

loading Core flash programming

( This is currently used by the ARM/v6-m, ARM/v7-m, and RISC-V targets.)

-- ------------------------------------------------------------------------
-- Verifying that host and target images are the same.
-- ------------------------------------------------------------------------
( Let's do verify first. That way flash-image can also verify.)

variable noisy-verify?  ( Whether to print differences.)
variable image-offset   ( offset to apply to host address to get target address)

| This sets up *two* pointers on the stack, ha [host address] and pad.
| Other implementations have used *one* pointer on the stack, and m for the
| other.

: setup-comparison  ( a u - ha pad u)
   push  pad over r@ t.read
   dup image+ tuck -  image-offset !
   pad pop ;

( Report differing address.)
| NOTE: the granularity of the display of addresses can be changed by
| looking at the bytes of diff and printing which parts have changed.

defer .differs  ( a diff)
defer .diff-addr  ( a)

( Peel off chunks of different sizes.)
: >hilo32  ( n - hi lo32)  dup 32 u>>  swap "ffff_ffff and ;
: >hilo16  ( n - hi lo16)  dup 16 u>>  swap      "ffff and ;

( By default, just print the entire differing address.)
-: ( a)  u. ;  is .diff-addr

( Verify with 4 byte granularity.)
-: ( a diff)  [ cell 4 / #] for  >hilo32 if  over .diff-addr  then  4 u+  next  2drop ;

( Verify with 2 byte granularity.)
-: ( a diff)   [ cell 2/ #] for  >hilo16 if  over .diff-addr  then  2 u+  next  2drop ;

( Verify with 1 byte granularity.)
-: ( a diff)           cell for  >hilo if    over .diff-addr  then  1 u+  next  2drop ;

( Words to choose the granularity that you want.)
: verify-1  [ #] is .differs ;  verify-1  ( default)
: verify-2  [ #] is .differs ;
: verify-4  [ #] is .differs ;

( Verify a host cell at a time, for speed and simplicity.)
: verify-cell  ( diff ha pad - diff' ha+ pad+)
   @+ push  swap @+ push  xor
   =if  noisy-verify? @ if
      r@ cell- image-offset @ +  over  .differs  then then
   or  pop pop ;

: verify-chunk  ( diff a u - diff' a+u)
   2dup + push
   aligned setup-comparison  cell/ for  verify-cell  next  2drop
   pop ;

| Verify using the same chunk size that we used for flashing; the variable
| /chunk - which is set by the interact code based on a value received from
| the debug interface - tells us how big each chunk should be.

: verify-region  ( - diff)
   noisy-verify? @ if
      cr ." Verifying " .region space
   then
   0  ( diff)  region
   /chunk @  /mod  ( r q)  swap push  for  /chunk @  verify-chunk  next
                                    pop  =if  ( rem) verify-chunk  drop ^  then  2drop ;

defer verify-regions  ( - diff)

: do-verify  ( - diff)  ?chat  h preserve  verify-regions ;

| Report whether region is empty, or, if non-empty, if it matched or
| mismatched host region.

: .region-message  ( z")
   warning-text  cr .region space  count type  normal-text ;

: report  ( diff - diff)
   region nip if  ( non-empty)
      =if  z" DIFFERS"  else  z" MATCHES"  then  .region-message ^
   then
   noisy-verify? @ if  ( only mention the EMPTY regions when being verbose)
      z" EMPTY"  .region-message
   then ;

| Here is a default version of verify-regions for targets with a single
| flash region.

( Verify and then report.)
-: flash verify-region  report ;  is verify-regions

: verify
   radix preserve hex  ( print mis-matched addresses in hex)
   noisy-verify? on  do-verify drop ;

| Called when we connect to the target to make sure that the host's and
| target's flash regions are the same.

-: noisy-verify? off  do-verify ;  is verify-quietly


-- ------------------------------------------------------------------------
-- Erasing and programing the flash
-- ------------------------------------------------------------------------

: program-chunk  ( 'target len)
   cr ."    program "  over u.  dup u.
   over image+ -rot  ( buf 'target len)  t.program ;

.ifdef /page  ( for devices with uniform page size)

: erase-page  ( 'target)
   cr ."    erase   "  dup u.
   t.erase ;

: erase?  ( 'target - erase?)   [ /page 1- #] and  0= ;

( Before we program the _first_ byte of a page, we erase the page.)
: flash-chunk  ( 'target len - 'target+len)
   2dup + push  over erase? if  over erase-page  then  program-chunk  pop ;

.else  ( non-uniform flash; uses sectors rather than pages)

: erase-sector  ( sector#)
   cr ."    erase sector "  dup u.
   t.erase ;

| Given a flash address, return true if on a sector boundary; also return
| sector# to erase; else return false.

: erase?  ( 'target - sector# -1 | 0)
   lookup-flash-sector ;

( Before we program the _first_ byte of a sector we erase the sector.)
: flash-chunk  ( 'target len - 'target+len)
   2dup + push  over erase? if  erase-sector  then  program-chunk  pop ;

.then

| Do the programming in chunks; the variable /chunk - which is set by the
| interact code based on a value received from the debug interface - tells
| us how big each chunk should be.

: flash-region
   cr ." Programming " .region
   region
   /chunk @  /mod  ( r q)  swap push  for  /chunk @  flash-chunk  next
                                    pop  =if  ( rem) flash-chunk  drop ^  then  2drop ;

defer ?flash-check    now nope is ?flash-check  ( default is do nothing)
defer flash-regions
defer after-flashing  ( diff)

: flash-image
   ?chat  h preserve  radix preserve hex  ( print messages in hex)
   ?flash-check  t.flash-begin  flash-regions  t.flash-end
   noisy-verify? on  verify-regions  after-flashing ;

| Here is a default version of flash-regions for targets with a single
| flash region.

-: flash flash-region ;  is flash-regions  ( very simple!)

( And a default for the post-flashing, post-verify action.)
-: drop ;                is after-flashing ( diff)


( XXX The following code probably needs to be moved. But whither?)

| The image reading and writing code expects a von Neumann memory model - a
| single address space. Let's only load this code if the ram and flash
| regions belong to the same aspace.

| Since not all targets are using the generic memory code that defines
| 'aspace, let's make sure to treat that case as well!

.ifdef 'aspace

: von-neumann?
   h preserve  ram 'aspace  flash 'aspace = ;

.else

( Not using the aspace code, so by definition the target is von Neumann!)
-1 constant von-neumann?

.then

von-neumann? .if

( Read an image from the target into the host image.)
: read-chunk  ( a u - a+u)
   -- cr ." read-chunk "  over u.  dup u.
   2dup + push
   over image+ -rot  t.read
   pop ;

: read-region  ( a u)
   /chunk @  /mod  ( r q)  swap push  for  /chunk @  read-chunk  next
                                    pop  =if  ( rem) read-chunk  drop ^  then  2drop ;

| If last 16 bytes contain "muforth@" followed by a starting target
| address, use that address rather than @flash.

: image-origin  ( a u - a u start)
   2dup +  cell- cell-  @+ @ swap
   [ token muforth@ drop @ #] = if
      ( valid signature) [ -2 cells #] u+ ^ then
   drop  @flash ( default to @flash) ;

| Read image from target. Leaves user in ram or flash depending on target
| address.

: read-image  ( target len)
   2dup read-region  over choose-image  over +  ( start end)  region! ;

| Load image from a file. Leaves user in ram or flash depending on image
| start address. If image does not contain a start address, leaves user in
| flash.

: load-image
   token, open-file-ro  ( fd)  dup read-file  ( a u)  rot close-file
   image-origin  dup choose-image
   2dup swap over +  ( start end)  region!
   ( a u start) image+ swap  cmove ;

| Save image to a file. Saves the current region. To make sure you are
| saving the flash region, do "flash save-image <name>".

: save-image
   token, create-file  ( fd)  push
   r@  region  swap image+ swap  write
   r@ " muforth@" write  r@ dp cell+ ( 'origin) cell write ( start)
   pop close-file ;

.then  ( von-neumann)
