| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

loading AVR UART test

__meta
hex

| Just stream bytes out TX initially, to make sure that chip is alive and
| well.

( Let's run the UART at 38,400 b/s. Baud rate divisor = fosc/bps/16 - 1.)
label uart-init
   #8,000,000 ( fosc = 8M)  #38,400 /  #16 /  1- x ldiw
   UBRR1H xh out  UBRR1L xl out
   %0001_1000 xl ldi  UCSR1B xl out  ( enable RX and TX)
   06 xl ldi  UCSR1C xl out  ( 8bits, 1stop, no parity)
   ret  ;c

( Host uses send and recv. Let's do the same here.)
label recv-byte
   ( Wait for RXC - receive complete)
   begin  UCSR1A xl in  7 ( RXC) xl sbrs  again
   UDR1 xl in  ret  ;c

( NOTE: clobbers xh)
label send-byte
   ( Wait for UDRE - data register empty)
   begin  UCSR1A xh in  5 ( UDRE) xh sbrs  again
   UDR1 xl out  ret  ;c

label spew
   40 xl ldi  begin  send-byte rcall  xl inc  5f xl andi  again  ;c

label echo
   begin  recv-byte rcall  send-byte rcall  again  ;c

label a-cmd   char A xl ldi  send-byte rjmp  ;c
label b-cmd   char B xl ldi  send-byte rjmp  ;c
label c-cmd   char C xl ldi  send-byte rjmp  ;c
label d-cmd   char D xl ldi  send-byte rjmp  ;c

( Testing dispatch)
label command-table
   a-cmd rjmp  b-cmd rjmp  c-cmd rjmp  d-cmd rjmp  ;c

label process
   recv-byte rcall  ( cmd)
   10 xl subi  ( 00 to 0f become large unsigned numbers)
   process command-table - 2/ xl cpi u< if  xh clr
      command-table u2/ ( word address) negate >hilo  xl subi  xh sbci
      xl push  xh push ( big-endian!)
   then ( unknown... return, and get called again)
   ret  ;c

RESET handler
   @ram #ram + 1- x ldiw  SPH xh out  SPL xl out
   uart-init rcall
   spew rjmp  ;c
   | echo rjmp  ;c
   | begin  process rcall  again  ;c
