| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

loading PIC18 flash programming

.ifdef 1xk50  64  .else  ( Q series)  256  .then
constant /page  ( erase page size)

ld target/common/flash.mu4

: prog-ee-chunk  ( 'target len - 'target+len)
   2dup + push
   over image+ -rot ( buf a u)  t.write-eeprom
   pop ;

: prog-ee-region
   [ ' erase-program-chunk >body #] preserve
   now prog-ee-chunk  is erase-program-chunk
   flash-region ;

| spi? returns true if we are programming over SPI. Since we can't write to
| the boot region over serial chat, we only program or verify the boot
| region when connected over SPI.

: spi?  ( - flag)  t.get-status  "dead_c0de = ;

-:          flash    flash-region
   spi? if  boot     flash-region  then
            user-id  flash-region
            config   prog-ee-region
            eeprom   prog-ee-region ;  is flash-regions

-: verify-2           flash   verify-region  report
             spi? if  boot    verify-region  report  or  then
                      user-id verify-region  report  or
   verify-1           config  verify-region  report  or
                      eeprom  verify-region  report  or ;  is verify-regions

-: 0= if  ( everything verified)
.ifdef -prog
      spi? if
         cr ." Taking target out of programming mode. Use 'spi' to re-enter programming mode."
         -prog  ^
      then
.then
      cr ." Restarting application"  t.app-start
   then ;  is after-flashing  ( diff)
