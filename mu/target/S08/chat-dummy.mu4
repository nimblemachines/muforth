( This file is part of muforth: https://muforth.nimblemachines.com/

  Copyright 2002-2018 David Frech. (Read the LICENSE for details.)

loading HC08 Dummy chat interface (for testing)

( NOTE: All the following d.Foobar commands are the dummy _implementations_
  of the chat command interface.)

variable dummy-image
: dimage+  ( 'target - 'host)  wrap  dummy-image @ + ;

variable dummy-sp
: dpush  ( sp w - sp-2)  -2 u+  over dimage+ beh! ;

( Frame: H CC A X PC)
: d.Hello
   ." Connecting to dummy chat target."
   here  dup dummy-image !  #image allot  #image "ff fill
   @ram #ram +
.ifdef QG8
   .ifdef in-ram
      "018a dpush  "0182 dpush  "2f60 dpush  "0268 dpush
   .else
      "fe95 dpush  "fe71 dpush  "2f60 dpush  "0268 dpush
   .then
.else  ( assume QE32)
   .ifdef in-ram
      "018a dpush  "0182 dpush  "4f80 dpush  "0868 dpush
   .else
      "fea4 dpush  "fe71 dpush  "4f80 dpush  "0868 dpush
   .then
.then
   dummy-sp ! ;

: d.ReadChunk    ( target len buf)
   -- ." dummy readchunk "  push  over u.  dup u.  r@ u.  pop
   push  swap dimage+ swap  pop swap  cmove ;

: d.WriteChunk   ( target len buf)
   -- ." dummy writechunk "  push  over u.  dup u.  r@ u.  pop
   -rot  swap dimage+ swap  cmove ;

: d.FlashChunk  ( target len buf fcmd - status)
   mPageErase = if
      -- ." dummy page erase "  push  over u.  dup u.  r@ u.  pop
      2drop  [ /page negate #] and  dimage+  /page  "ff fill  "c0  ^  then
   d.WriteChunk  "c0 ;

: d.GetRegs   ( frame - sp)
   push  dummy-sp @ dup  6  pop d.ReadChunk ;

: d.SetRegs   ( frame)
         dummy-sp @      6  rot d.WriteChunk ;

: chat
   chat-via  d.Hello  d.GetRegs  d.SetRegs  nope ( Run)
             d.ReadChunk  d.WriteChunk  d.FlashChunk ;
