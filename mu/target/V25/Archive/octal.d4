( octal.4d)

( octal dump -- originally for looking at PilotFORTH; modified for target
  compilation, esp on 80x86.)

( This is really a -generic- dump;  it should dump in hex, octal words,
  and then chars.)

( 06-apr-1999. Changed to be smart about the addresses it prints, so that
  dumps of target-compiled data will have meaningful addresses printed.
  The `clients' of this program need to define the word  `|+'  to get from
  target address to image address.)

cr  ." (( Octal dump "

decimal

( Memory dump)
: |c@+  ( |a - |a+1 ch)   1+ dup  |+ 1- c@  ;
: .ob   ( b)       octal  0 <#         # # #   #>  type  ;
: .a-space   ( a - a)  10 spaces  ;  ( .w + 2 spaces)
: .padding   ( a - a)
   dup  15 and  dup 2* 2*  swap 2/ 2/ +  spaces  ;

: #bytes     ( a - a #bytes)  16 over  15 and -  ;
: spacing    ( a - a)  1 over 3 and 0= -  spaces  ;
: >graphic  ( ch - ch')  dup "7f and 32 u< if  drop  char .  then  ;

: .chars  ( a)      dup .a        .padding   #bytes for
   |c@+ ( a+1 ch)  2 spaces  >graphic emit  spacing  next  drop  ;

: .hex-bytes  ( a)       cr  .a-space .padding   #bytes for
   |c@+ ( a+1 ch)  space  .b   spacing  next drop  ;

: .octal-bytes  ( a - a+)       cr  .a-space .padding   #bytes for
   |c@+ ( a+1 ch)  .ob hex  spacing  next  ;

: .line   ( a - a+)   dup .chars  dup .hex-bytes  .octal-bytes   ;

: od  ( |a - |a+)  radix @ push  hex   begin  .line  key
   10 xor until   pop radix !  ;

( Should this leave the a+ ?)


." ))"
