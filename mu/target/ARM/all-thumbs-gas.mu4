( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

loading Generating Thumb instructions for gas

( Generate GNU assembler code that "represents" every possible 16-bit Thumb
| instruction, and a subset of the 32-bit Thumb instructions.)

hex

( Output halfword - 16 bits, in unix style hex: 0xabcd)
: .h  <#  # # # #  char x hold  char 0 hold  #>  type ;

( Initially just output all instructions from 0000 to e7ff - these are all
| the possible 16-bit Thumb instructions!)

: .prefix  ( index)
   7 and 0= if  cr  ." .short "  ^ then  char , emit  space ;

: .halfwords
   0e800 0 do  i .prefix  i .h  loop ;

: ,h   1 .prefix  .h ;  ( output , then value)
: .words
   1_0000 0e800 do
      0 .prefix  i .h     0f ,h
                 i ,h    0f0 ,h
                 i ,h   0f00 ,h
                 i ,h  0f000 ,h
   loop ;

: gen-image
   radix preserve  hex
   out-channel preserve  >stdout
   cr  ." .text"
   cr  ." .thumb_func"
   cr  ." all_thumbs:"
  .halfwords
  .words
   cr ." .end"
   cr
;

gen-image
