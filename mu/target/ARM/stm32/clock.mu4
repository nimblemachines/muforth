( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)

( Some utility words related to setting up the clock[s] on the STM32.)

loading STM32 clock setting

hex
__meta

( Set a byte register to a value [set], then loop, reading the same
| register, until the register anded with a mask matches the match value.)

: set-until-match  ( set mask match reg)
   >r  rot r@ c!  ( set reg to "set" value)
   begin  over r@ c@ and  over =  until  r>  drop drop drop ;

: sysclk!  ( clksrc)  0f  over 5 *  RCC_CFGR  set-until-match ;

: >hsi  0 ( SW=HSI) sysclk! ;
: >hse  1 ( SW=HSE) sysclk! ;
: >pll  2 ( SW=PLL) sysclk! ;

: hse-start   ( hsebits)  2 2  [ RCC_CR 2 + #]  set-until-match ;
: +hse         1 ( HSEON)           hse-start ;
: +hse-bypass  5 ( HSEON + HSEBYP)  hse-start ;

: +pll   1  2 2  [ RCC_CR 3 + #]  set-until-match ;
: -pll   0  2 0  [ RCC_CR 3 + #]  set-until-match ;
