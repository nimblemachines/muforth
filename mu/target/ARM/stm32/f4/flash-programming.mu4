| This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2025 David Frech. (Read the LICENSE for details.)

loading STM32F4 flash programming

hex
__meta

label stm32-flash-unlock
   { x w } pop   FLASH_KEYR >fr w str
                 FLASH_KEYR >fr x str
   0 w movs      FLASH_CR   >fr w str  ( clear any program/erase bits)
   FLASH_SR >fr w ldrh   FLASH_SR >fr w strh  ( clear any error bits)
   lr bx  ;c

label stm32-flash-lock
   80 ( LOCK) w movs   FLASH_CR 3 + >fr w strb  ( lock CR)
   lr bx  ;c

label stm32-flash-page-erase  ( page#)
   { w } pop   3 w w lsls  ( shift sector# up 3 bits)
   2 ( PER)  w w adds   FLASH_CR     >fr w str  ( set PER + page number)
   1 ( STRT) w movs     FLASH_CR 2 + >fr w strb ( set STRT bit)
   ( fall thru)  ;c

( Wait until flash command finishes. Clobbers w.)
label -busy
   begin   FLASH_SR 2 + >fr w ldrb   w w tst   0= until
   lr bx  ;c

| F4 can be programmed by byte, halfword, word, or double-word, but
| double-word programming requires an external high voltage power supply,
| so we program it by word.

| w is byte count, x is flash address,
| y is ram buffer address, z is offset.
label stm32-flash-program  ( buf a u)
   1 ( PG)    w movs   FLASH_CR    >fr w str  ( set PG bit)
   2 ( psize) w movs   FLASH_CR 1+ >fr w strb ( psize=2; program by 32bit word)
   { y x w } pop
   { lr } push
   0 z movs  ( offset)   w cnt mov  ( save)
   begin
      ( 32-bit word at a time.)
      z y w ldr   z x w str   4 z z adds
      -busy bl  ( clobbers w)
      cnt z cmp
   u>= until
   { pc } pop  ;c
