( This file is part of muforth: https://muforth.dev/
|
| Copyright 2002-2024 David Frech. (Read the LICENSE for details.)


loading Generation of C image

( Code to generate C code that "represents" the contents of the
| meta-compiled image, and also the relocation bitmap.)

forth decimal

( Print a C-style 32-bit hex number.)
: .h  radix preserve hex
      <#  # # # #  # # # #  char x hold  char 0 hold #>  type ;

: .w,  .h  ." , " ;

: .a  ." /* "  \m cells  .h  ." : */  "  ;

( Print a decimal number with no separators and no following space.)
: .d  radix preserve decimal  <# #s #> type ;

: .words  ( a n)  0 -rot  for
   over 3 and 0= if cr over .a then  1 u+
   dup lew@ .w,  \m cell+  next  2drop ;

: gen-c-image
   out-channel preserve  >stdout
   cr ." int image[] = {"
      image  \m here  \m cell/  .words
   cr ." };"

   cr
   cr ." int reloc_bitmap[] = {"
      reloc-bitmap  \m here  \m cell/ 31 + 5 >>  tuck  ( ncells 'reloc ncells)
      .words
   cr ." };"
   cr

   cr ." int image_sz = " \m here .d  ." ;"
   cr ." int reloc_ncells = " .d  ." ;"
   cr
;
